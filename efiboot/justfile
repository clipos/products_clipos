# SPDX-License-Identifier: LGPL-2.1-or-later
# Copyright Â© 2017-2018 ANSSI. All rights reserved.

product = "clipos"
recipe = "efiboot"

# Pipeline all the actions (from build to bundling going through configuration)
# to produce the final result of this recipe
all: build image configure bundle

# Pipeline all the actions but without reusing any previously build byproducts
all-from-scratch: clear-cache all

# Build the binary packages necessary to build the rootfs of this recipe from
# scratch
build-from-scratch: clear-cache build

# Make sure the SDK is available
sdk:
    just ../sdk/all

# Build the binary packages necessary to build the rootfs of this recipe
build: clear-build sdk
    cosmk build "{{product}}/{{recipe}}" --no-clear-previous-build

# Assemble the binary packages built during the "build" action into a rootfs
# (therefore purged from any build-time dependencies)
image: clear-image sdk
    cosmk image "{{product}}/{{recipe}}"

# Tweak the configuration of the rootfs produced during the "image" action for
# the final CLIP image assembly
configure: clear-configure sdk
    cosmk configure "{{product}}/{{recipe}}"

# Bundle the rootfs produced during the "configure" action into a file format
# that can be handled during the final CLIP image assembly
bundle: clear-bundle sdk
    cosmk bundle "{{product}}/{{recipe}}"

# Clear the binary package cache used during the "build" action step. This will
# force the build from scratch of all the packages on the next run of the
# "build" action step.
clear-cache:
    #!/usr/bin/env bash
    repo_root="$(cosmk repo-root-path)"
    version="$(cosmk product-version {{product}})"
    rm -rf "${repo_root}/cache/{{product}}/${version}/{{recipe}}"

# Clear the result of the previous "build" action step.
clear-build:
    #!/usr/bin/env bash
    repo_root="$(cosmk repo-root-path)"
    version="$(cosmk product-version {{product}})"
    rm -rf "${repo_root}/out/{{product}}/${version}/{{recipe}}/build"

# Clear the result of the previous "image" action step.
clear-image:
    #!/usr/bin/env bash
    repo_root="$(cosmk repo-root-path)"
    version="$(cosmk product-version {{product}})"
    rm -rf "${repo_root}/out/{{product}}/${version}/{{recipe}}/image"

# Clear the result of the previous "configure" action step.
clear-configure:
    #!/usr/bin/env bash
    repo_root="$(cosmk repo-root-path)"
    version="$(cosmk product-version {{product}})"
    rm -rf "${repo_root}/out/{{product}}/${version}/{{recipe}}/configure"

# Clear the result of the previous "bundle" action step.
clear-bundle:
    #!/usr/bin/env bash
    repo_root="$(cosmk repo-root-path)"
    version="$(cosmk product-version {{product}})"
    rm -rf "${repo_root}/out/{{product}}/${version}/{{recipe}}/bundle"

# vim: set ts=4 sts=4 sw=4 et ft=make:
